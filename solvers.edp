macro Stokes()
{
  solve st([u1, u2, p], [v1, v2, q]) =
    int2d(Th)(2 * mu * tr(EPS(u1,u2)) * EPS(v1,v2) - p * div(v1,v2) - q * div(u1,u2))
    - int2d(Th)(p * q * 1e-8)
    + on(3, u1 = 0, u2 = 0) 
    + on(1, u1 = udc1, u2 = udc2);
} //EOM


macro AdjointStokes() 
{
  solve ad([w1, w2, qq], [v1, v2, q]) =
    int2d(Th)(2 * mu * tr(EPS(w1, w2)) * EPS(v1, v2) - qq * div(v1, v2) - q * div(w1, w2))
    + int2d(Th)(-4 * mu * tr(EPS(u1, u2)) * EPS(v1, v2))
    - int2d(Th)(qq * q * 1e-8)
    + on(1, 3, w1 = 0, w2 = 0);
}//EOM


/* dL: Derivative of Lagrange (objective) function */
macro RegExLaplaceBeltrami() 
{
  solve re([tta1, tta2], [eta1, eta2]) =
    int2d(Th)(gm * tr(EPS(tta1, tta2)) * EPS(eta1, eta2))
    + int1d(Th,3)((1 - gm) * (tr(gradT(tta1)) * gradT(eta1) + tr(gradT(tta2)) * gradT(eta2)))
    + int1d(Th,3)(dL * tr([eta1, eta2]) * [N.x, N.y])
    + on(1, 2, 4, tta1 = 0, tta2 = 0);
} //EOM

macro RegExElliptic() 
{
  solve re([tta1, tta2], [eta1, eta2]) =
    int2d(Th)(gm * tr(EPS(tta1, tta2)) * EPS(eta1, eta2))
	  + int2d(Th)(tta1 * eta1 + tta2 * eta2)
    + int1d(Th, 3)(dL * tr([eta1, eta2]) * [N.x, N.y])
    + on(1, 2, 4, tta1 = 0, tta2 = 0);
} //EOM


macro Streamlines()
{
  solve sl(psi, phi) =
    int2d(Th)(dx(psi) * dx(phi) + dy(psi) * dy(phi))
    + int2d(Th)(-phi*(dy(u1) - dx(u2)))
    + on(1, 2, 3, psi = 0);
}//EOM


macro Elastic()
{
  solve el([u1, u2], [v1, v2]) =
    int2d(Th)(lambda * div(u1, u2) * div(v1, v2) + 2.0 * mu * (tr(EPS(u1, u2)) * EPS(v1,v2))) 
    - int1d(Th, 2)(g1 * v1 + g2 * v2)
    - int1d(Th, 4)(gg1 * v1 + gg2 * v2)
    + on(1, u1 = 0, u2 = 0);
} //EOM