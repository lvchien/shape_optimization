load "medit";
include "getARGV.idp"
include "operators.edp"


/* Label of borders */
int FREE = 1;                                                               // Free boundary
int FWALL = 11;                                                             // Homogeneous Dirichlet boundary of fluid domain
int FIN = 12;                                                               // Non-homogeneous Dirichlet boundary of fluid domain
int FOUT = 13;                                                              // Neumann boundary of fluid domain
int SWALL = 21;                                                             // Homogeneous Dirichlet boundary of solid domain
int SLOAD = 22;                                                             // Neumann boundary of solid domain
int DUMMY = 30;                                                             // Dummy boundary


/* Constants */
real epsilon = 1e-8;

/*----------------------------------- Problem configurations -----------------------------------*/
int case = getARGV("--case", 1);                                            // Test case
real nu = getARGV("--nu", 0.05);                                            // The kinematic viscosity coefficient of fluid
real E = getARGV("--E", 21.0);                                              // The Young modulus of solid
real nus = getARGV("--nus", 0.3);                                           // The Poisson ratio of solid
real vf = getARGV("--vf", 1.) ;                                             // Volume fraction
real w = getARGV("--w", 1.0);                                               // Weight parameter of the objective functions
/*----------------------------------------------------------------------------------------------*/

/*---------------------------------- Numerical configurations ----------------------------------*/                                                                
real b0 = getARGV("--b0", 1e-1);                                            // Initial value of b
real bmax = getARGV("--bmax", 1e2);                                         // Maximal value of b, no longer update if b > bmax
real l0 = getARGV("--l0", 0.0);                                             // Initial value of l
real gm = getARGV("--gm", 0.01);                                            // Regularization parameter 
real tau0 = getARGV("--tau0", 1e-1);                                        // Initial descent step length
real beta = getARGV("--beta", 0.7);                                         // Update step length 
real sm = getARGV("--sm", 1e-4);                                            // Armijo condition's coefficient
real alpha = getARGV("--alpha", 1.05);                                      // Update coefficient of b: b_{n+1} = alpha*b_n
real minsgn = getARGV("--minsgn", 1e-3);                                    // Value for the stopping criterion
real minarea = getARGV("--minarea", 1e-6);                                  // Remesh parameter: remesh if any element has an area less than minarea
real mshsize = getARGV("--mshsize", 0.02);                                  // Required edge size when remeshing : hmax = mshsize, hmin = mshsize/sqrt(2)
/*--------------------------------------------------------------------------------------------*/


/*------------------------------------ IO configurations -------------------------------------*/
string fmsh = getARGV("--fmsh", "fluid");                                   // File name of fluid domain's mesh
string smsh = getARGV("--smsh", "solid");                                   // File name of solid domain's mesh
string dmsh = getARGV("--dmsh", "solid");                                   // File name of whole domain's mesh
string folder = getARGV("--folder", "results");                             // folder contains output
int steps = getARGV("--steps", 3);                                          // number of steps to save results once
/*--------------------------------------------------------------------------------------------*/

system("mkdir " + folder);
ofstream fout(folder + "/out.dat");

/* Save the command */
ofstream cmd(folder + "/command.sh");
for (int i = 0; i < ARGV.n; i++)
    cmd << ARGV[i] << " ";
cmd.flush;

/* Load mesh */
mesh Fh = readmesh("meshes/FSI/" + fmsh + ".mesh");
mesh Sh = readmesh("meshes/FSI/" + smsh + ".mesh");
mesh Th = readmesh("meshes/FSI/" + dmsh + ".mesh");
plot(Fh, Sh);
cout << "Done load meshes!" << endl;

/* Declare FE spaces */
fespace FVh(Fh, P2);
fespace FPh(Fh, P1);
fespace SUh(Sh, P1);
fespace Dh(Th, P1);


/* Declare FE variables */
FVh v1, v2, va1, va2, vt1, vt2, ff1, ff2, vin1, vin2, vw1, vw2, vout1, vout2;
FPh p, pa, pt;
SUh u1, u2, ua1, ua2, ut1, ut2, fs1, fs2, uload1, uload2;
Dh d1, d2, dt1, dt2;


/*-------------------------- Set initial values for each test case --------------------------*/
if(case == 1)
{
    ff1 = 0.0; ff2 = 0.0;
    vin1 = 16 * y * (0.5 - y); vin2 = 0.0;
    vout1 = 0.0; vout2 = 0.0;

    fs1 = 0.0; fs2 = 0.0;

    E = 0.1; nu = 0.005; nus = 0.05;
}
else if(case == 2)
{
    ff1 = 0.0; ff2 = 0.0;
    vin1 = 1.0; vin2 = 0.0;
    vw1 = 0.0; vw2 = 0.0;
    vout1 = 0.0; vout2 = 0.0;

    fs1 = 0.0; fs2 = 0.0;
    uload1 = 0.0; uload2 = 0.0;

}
else if(case == 3)
{
    ff1 = 0.0; ff2 = 0.0;
    vin1 = 4.0 * y * (1.0 - y); vin2 = 0.0;
    vw1 = 0.0; vw2 = 0.0;
    vout1 = 0.0; vout2 = 0.0;

    fs1 = 0.0; fs2 = 0.0;
    uload1 = 0.0; uload2 = 0.0;

    E = 15.0; nu = 0.01; nus = 0.35;
}
/*--------------------------------------------------------------------------------------------*

/* Variables */
real mu = E/(2 * (1 + nus));
real lambda = E * nus/((1 + nus) * (1 - 2 * nus));
real l = l0;
real b = b0;
real J0, vol0, convol;

/*-------------------------- Declare functions and their derivatives --------------------------*/
macro compliance() (int2d(Sh)(2.0 * mu * dot(eps2(u1, u2), eps2(u1, u2)) + lambda * div2(u1, u2) * div2(u1, u2))) //EOM
macro dissipatedenergy() (int2d(Fh)(2.0 * nu * dot(eps2(v1, v2), eps2(v1, v2)))) //EOM
macro J() (w * compliance + (1 - w) * dissipatedenergy) //EOM
macro dJ() (w * (2.0 * mu * dot(eps2(u1, u2), eps2(u1, u2)) + lambda * div2(u1, u2) * div2(u1, u2))
            + 2.0 * (1 - w) * nu * dot(eps2(v1, v2), eps2(v1, v2))
            - (fs1 * ua1 + fs2 * ua2) + (ff1 * va1 + ff2 * va2)
            + 2.0 * mu * dot(eps2(u1, u2), eps2(ua1, ua2)) + lambda * div2(u1, u2) * div2(ua1, ua2)
            - 2.0 * nu * dot(eps2(v1, v2), eps2(ua1, ua2)) + p * div2(ua1, ua2)
            + 2.0 * nu * dot(eps2(v1, v2), eps2(va1, va2))) //EOM

macro vol(Th) (int2d(Th)(1.)) //EOM
macro dvol() (1.0) //EOM

macro L() (J/J0 - l * (vol(Sh) - convol)/vol0 + b/2.0 * ((vol(Sh) - convol) ^ 2)/(vol0 ^ 2)) //EOM 
macro dL() (dJ/J0 - l * dvol/vol0 + b * dvol * (vol(Sh) - convol)/(vol0 ^ 2)) //EOM
/*---------------------------------------------------------------------------------------------*/


/*-------------------------------- Declare variational problems --------------------------------*/
problem stokes2D([v1, v2, p], [vt1, vt2, pt]) = 
    int2d(Fh)(2.0 * nu * dot(eps2(v1, v2), eps2(vt1, vt2)) - p * div2(vt1, vt2) - pt * div2(v1, v2))
        - int2d(Fh)(ff1 * vt1 + ff2 * vt2)
        - int2d(Fh)(p * pt * epsilon)
        - int1d(Fh, FOUT)(vout1 * vt1 + vout2 * vt2)
        + on(FIN, v1 = vin1, v2 = vin2)
        + on(FWALL, SWALL, FREE, v1 = 0.0, v2 = 0.0);

problem elastic2D([u1, u2], [ut1, ut2]) = 
    int2d(Sh)(2.0 * mu * dot(eps2(u1, u2), eps2(ut1, ut2)) + lambda * div2(u1, u2) * div2(ut1, ut2))
        - int2d(Sh)(fs1 * ut1 + fs2 * ut2)
        - int1d(Sh, SLOAD)(uload1 * ut1 + uload2 * ut2)
        - int1d(Sh, FREE)(((2 * nu * dx(v1) - p) * N.x + nu * (dy(v1) + dx(v2)) * N.y) * ut1 + (nu * (dy(v1) + dx(v2)) * N.x + (2 * nu * dy(v2) - p) * N.y) * ut2)
        + on(SWALL, u1 = 0.0, u2 = 0.0);

problem adjointelastic2D([ua1, ua2], [ut1, ut2]) = 
    int2d(Sh)(2.0 * mu * dot(eps2(ua1, ua2), eps2(ut1, ut2)) + lambda * div2(ua1, ua2) * div2(ut1, ut2))
        + int2d(Sh)(2 * w * (2.0 * mu * dot(eps2(u1, u2), eps2(ut1, ut2)) + lambda * div2(u1, u2) * div2(ut1, ut2)))
        + on(SWALL, ua1 = 0.0, ua2 = 0.0);

problem adjointstokes2D([va1, va2, pa], [vt1, vt2, pt]) = 
    int2d(Fh)(2.0 * nu * dot(eps2(va1, va2), eps2(vt1, vt2)) - pa * div2(vt1, vt2) - pt * div2(va1, va2))
        + int2d(Fh)(4.0 * (1 - w) * nu * dot(eps2(v1, v2), eps2(vt1, vt2)))
        - int2d(Fh)(pa * pt * epsilon)
        + on(FWALL, SWALL, FIN, va1 = 0.0, va2 = 0.0)
        + on(FREE, va1 = ua1, va2 = ua2);

problem solveregex([d1, d2], [dt1, dt2]) = 
  int2d(Th)(gm * dot(Jaco2(d1, d2), Jaco2(dt1, dt2)) + dot([d1, d2], [dt1, dt2]))
       + int1d(Sh, FREE)(dL * dot([dt1, dt2], N2))
       + on(SLOAD, SWALL, FWALL, FIN, FOUT, DUMMY, d1 = 0.0, d2 = 0.0);

problem solveregex1([d1, d2], [dt1, dt2]) = 
     int2d(Th)(2 * mu * dot(eps2(d1, d2), eps2(dt1, dt2)) + lambda * div2(d1, d2) * div2(dt1, dt2))
        + int1d(Sh, FREE)(dL * dot([dt1, dt2], N2))
        + on(SLOAD, SWALL, FWALL, FIN, FOUT, DUMMY, d1 = 0.0, d2 = 0.0);

macro solvestate()
{
    stokes2D;
    elastic2D;
} //EOM

macro solveadjoint()
{
    adjointelastic2D;
    adjointstokes2D;
} //EOM

/*----------------------------------------------------------------------------------------------*/

/*----------------------------------------- Main block -----------------------------------------*/
/* Initial values */
vol0 = vol(Sh) + vol(Fh);
convol = vol0 * vf;
solvestate;
J0 = J;
fout << compliance << " " <<  L << " " << vol(Sh) - convol << " " << l0 << endl;
fout.flush;

int imax = 1000;
for(int i = 0; i < imax; i++)
{
    solveadjoint;
    solveregex;
    real dd = int1d(Sh, FREE)(dL * dot([d1, d2], N2));
    cout << "Directional derivative: " << dd << endl;

    real sgn = sqrt(int1d(Sh, FREE)(d1^2 + d2^2));
    if(sgn > minsgn)
    {
        Fh = adaptmesh(Fh, hmax = mshsize, hmin = mshsize / sqrt(2), ratio = 1.5);
        Sh = adaptmesh(Sh, hmax = mshsize, hmin = mshsize / sqrt(2), ratio = 1.5);
        plot(Th, [d1, d2], cmm = "Iteration i = " + i + "/" + imax);

        real tau = tau0;
        real fminarea = checkmovemesh(Fh, [x + tau * d1, y + tau * d2]);
        real sminarea = checkmovemesh(Sh, [x + tau * d1, y + tau * d2]);

        int j = 0;
        int jmax = 40;

        while(j < jmax && !(fminarea >= minarea && sminarea >= minarea))
        {
            j++;
            cout << "Iteration j = " << j << "/" << jmax << ": Step length = " << tau << endl;
            tau *= beta;
            fminarea = checkmovemesh(Fh, [x + tau * d1, y + tau * d2]);
            sminarea = checkmovemesh(Sh, [x + tau * d1, y + tau * d2]);
        }

        if(j < jmax)
        {
            mesh tmpFh = Fh;
            mesh tmpSh = Sh;
            real beforeL = L;
            
            Fh = movemesh(Fh, [x + tau * d1, y + tau * d2]);
            Sh = movemesh(Sh, [x + tau * d1, y + tau * d2]);
            solvestate;
            real afterL = L;

            int k = 0;
            int kmax = 40;

            while(k < kmax && !(afterL < beforeL + sm * tau * dd))
            {
                k++;
                cout << "Iteration k = " << k << "/" << kmax << ": Step length = " << tau << endl;
                tau *= beta;
                Fh = tmpFh;
                Sh = tmpSh;

                Fh = movemesh(Fh, [x + tau * d1, y + tau * d2]);
                Sh = movemesh(Sh, [x + tau * d1, y + tau * d2]);
                plot(Fh, Sh,cmm = "Iteration i = " + i + "/" + imax);
                solvestate;
                afterL = L;
            }

            if(k < kmax)
            {
                l = l - b * (vol(Sh) - convol)/vol0;
                b = (b < bmax) ? b * alpha : bmax;
            }
            else
            {
                cout << "The shape has not been changed significant!" << endl;
                break;
            }
        }
        else
        {
            cout << "The descent direction is invalid!" << endl;
            break;
        }

        /* Save post-processing data */
        fout << compliance << " " <<  L << " " << vol(Sh) - convol << " " << l << endl;
        fout.flush;
        if(i % steps == 0)
        {
            savesol(folder + "/fmesh." + i/steps + ".sol", Fh, [v1, v2]);
            savesol(folder + "/smesh." + i/steps + ".sol", Sh, [u1, u2]);
            savemesh(Fh, folder + "/fmesh." + i/steps + ".mesh");
            savemesh(Sh, folder + "/smesh." + i/steps + ".mesh");
        }
    }
    else
    {
        cout << "The shape has not been changed significant!" << endl;
        break;
    }
}

/*----------------------------------------------------------------------------------------------*/

// Final results
savesol(folder + "/fmesh_final.sol", Fh, [v1, v2]);
savesol(folder + "/smesh_final.sol", Sh, [u1, u2]);
savemesh(Fh, folder + "/fmesh_final.mesh");
savemesh(Sh, folder + "/smesh_final.mesh");