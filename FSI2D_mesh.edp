include "getARGV.idp"

int case = getARGV("--case", 1);                                // Case study
string fmsh = getARGV("--fmsh", "fluid");                       // File name of the mesh of fluid domain
string smsh = getARGV("--smsh", "solid");                       // File name of the mesh of solid domain
int nps = getARGV("--nps", 3);                                  // Number of meshing points

/* Label of borders */
int FREE = 1;
int FWALL = 11;
int FIN = 12;
int FOUT = 13;
int SWALL = 21;
int SDEFO = 22;
int SLOAD = 23;

mesh Fh, Sh;
system("mkdir " + "meshes/FSI");

/* Build meshes */
if(case == 1)
{
    border FW1(t = 0.0, 0.3){x = t; y = 0.0; label = FWALL;}
    border F1(t = 0.0, 0.3){x = 0.3; y = t; label = FREE;}
    border F2(t = 0.3, 0.7){x = t; y = 0.3; label = FREE;}
    border F3(t = 0.3, 0.0){x = 0.7; y = t; label = FREE;}
    border FW2(t = 0.7, 2.0){x = t; y = 0.0; label = FWALL;}
    border OUT(t = 0.0, 0.5){x = 2.0; y = t; label = FOUT;}
    border FW3(t = 2.0, 0.0){x = t; y = 0.5; label = FWALL;}
    border IN(t = 0.5, 0.0){x = 0.0; y = t; label = FIN;}
    border SW(t = 0.3, 0.7){x = t; y = 0.0; label = SWALL;}

    Fh = buildmesh(FW1(3 * nps) + F1(9 * nps) + F2(12 * nps) + F3(9 * nps) + FW2(13 * nps) + OUT(5 * nps) + FW3(20 * nps) + IN(5 * nps));
    Sh = buildmesh(SW(8 * nps) + F3(-9 * nps) + F2(-12 * nps) + F1(-9 * nps));
}
else if(case == 2)
{
    border FW1(t = 0.0, 2.0){x = t; y = 1.0; label = FWALL;}
    border FW2(t = 1.0, 0.0){x = 2.0; y = t; label = FWALL;}
    border FW3(t = 2.0, 7.0){x = t; y = 0.0; label = FWALL;}
    border OUT(t = 0.0, 3.0){x = 7.0; y = t; label = FOUT;}
    border FW4(t = 7.0, 2.0){x = t; y = 3.0; label = FWALL;}
    border FW5(t = 3.0, 2.0){x = 2.0; y = t; label = FWALL;}
    border FW6(t = 2.0, 0.0){x = t; y = 2.0; label = FWALL;}
    border IN(t = 2.0, 1.0){x = 0.0; y = t; label = FIN;}
    border SW1(t = 3.0, 3.5){x = t; y = 0.5; label = SWALL;}
    border F1(t = 0.5, 2.5){x = 3.5; y = t; label = FREE;}
    border SW2(t = 3.5, 3.0){x = t; y = 2.5; label = SWALL;}
    border F2(t = 2.5, 0.5){x = 3.0; y = t; label = FREE;}

    Fh = buildmesh(FW1(4 * nps) + FW2(2 * nps) + FW3(10 * nps) + OUT(6 * nps) + FW4(10 * nps) + FW5(2 * nps) + FW6(4 * nps) + IN(2 * nps) + SW1(-1 * nps) + F1(-4 * nps) + SW2(-1 * nps) + F2(-4 * nps));
    Sh = buildmesh(SW1(1 * nps) + F1(4 * nps) + SW2(1 * nps) + F2(4 * nps));
}
else if(case == 3)
{
    border FW1(t = 0.0, 2.0){x = t; y = 0.0; label = FWALL;}
    border FW2(t = 2.9, 4.0){x = t; y = 0.0; label = FWALL;}
    border OUT(t = 0.0, 0.8){x = 4.0; y = t; label = FOUT;}
    border FW3(t = 4.0, 0.0){x = t; y = 0.8; label = FWALL;}
    border IN(t = 0.8, 0.0){x = 0.0; y = t; label = FIN;}
    border SW(t = 2.0, 2.9){x = t; y = 0.0; label = SWALL;}
    border F1(t = 0.0, 0.7){x = 2.9; y = t; label = FREE;}
    border F2(t = 2.9, 2.0){x = t; y = 0.7; label = FREE;}
    border F3(t = 0.7, 0.0){x = 2.0; y = t; label = FREE;}

    Fh = buildmesh(FW1(20 * nps) + F3(-7 * nps) + F2(-9 * nps) + F1(-7 * nps) + FW2(11 * nps) + OUT(8 * nps) + FW3(40 * nps) + IN(8 * nps));
    Sh = buildmesh(SW(9 * nps) + F1(7 * nps) + F2(9 * nps) + F3(7 * nps));
}

savemesh(Fh, "meshes/FSI/" + fmsh + ".mesh");
savemesh(Sh, "meshes/FSI/" + smsh + ".mesh");
plot(Fh, Sh);